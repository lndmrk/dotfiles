# SPDX-FileCopyrightText: 2019 Emil Lundmark <emil@lndmrk.se>
# SPDX-License-Identifier: GPL-3.0-or-later
#+TITLE: lndmrk's Emacs Configuration
#+AUTHOR: Emil Lundmark

* Defaults

** User Interface

#+begin_src emacs-lisp
(use-package emacs
  :init
  (horizontal-scroll-bar-mode -1)
  (menu-bar-mode -1)
  (scroll-bar-mode -1)
  (tool-bar-mode -1)
  :custom
  (inhibit-startup-echo-area-message (user-login-name))
  (inhibit-startup-screen t)
  (initial-scratch-message nil))
#+end_src

#+begin_src emacs-lisp
(use-package server
  :custom (server-client-instructions nil))
#+end_src

** Backup Files

#+begin_src emacs-lisp
(use-package files
  :preface
  (defun my-backup-enable-p (name)
    (when (normal-backup-enable-predicate name)
      (let ((backup-ignore-regexp-list '("\\.asc$" "\\.gpg$" "^/dev/shm"))
            (result t))
        (dolist (regexp backup-ignore-regexp-list result)
          (setq result (and result (not (string-match regexp name))))))))
  :init
  (let ((backup-directory
         (file-name-as-directory (locate-user-emacs-file "backups"))))
    (unless (file-directory-p backup-directory)
      (make-directory backup-directory t))
    (setopt backup-directory-alist `((".*" . ,backup-directory))))
  (setq backup-enable-predicate #'my-backup-enable-p)
  :custom
  (find-file-visit-truename t)
  :config
  (auto-save-visited-mode 1))
#+end_src

** Custom File

#+begin_src emacs-lisp
(use-package emacs
  :custom (custom-file (locate-user-emacs-file "custom.el"))
  :init (when (file-exists-p custom-file) (load custom-file)))
#+end_src

** Key Bindings

*** Unbind Aliases

#+begin_src emacs-lisp
(use-package emacs
  :config
  (unbind-key "<begin>")
  (unbind-key "<deletechar>")
  (unbind-key "<down>")
  (unbind-key "<end>")
  (unbind-key "<home>")
  (unbind-key "<insert>")
  (unbind-key "<insertchar>")
  (unbind-key "<left>")
  (unbind-key "<next>")
  (unbind-key "<prior>")
  (unbind-key "<right>")
  (unbind-key "<up>")
  (unbind-key "C-<backspace>")
  (unbind-key "C-<delete>")
  (unbind-key "C-<down>")
  (unbind-key "C-<end>")
  (unbind-key "C-<home>")
  (unbind-key "C-<insert>")
  (unbind-key "C-<insertchar>")
  (unbind-key "C-<left>")
  (unbind-key "C-<right>")
  (unbind-key "C-<up>")
  (unbind-key "C-M-<down>")
  (unbind-key "C-M-<end>")
  (unbind-key "C-M-<home>")
  (unbind-key "C-M-<left>")
  (unbind-key "C-M-<right>")
  (unbind-key "C-M-<up>")
  (unbind-key "C-c <left>")
  (unbind-key "C-c <right>")
  (unbind-key "C-x <left>")
  (unbind-key "C-x <right>")
  (unbind-key "C-x C-<left>")
  (unbind-key "C-x C-<right>")
  (unbind-key "M-<begin>")
  (unbind-key "M-<end>")
  (unbind-key "M-<home>")
  (unbind-key "M-<left>")
  (unbind-key "M-<next>")
  (unbind-key "M-<prior>")
  (unbind-key "M-<right>")
  (unbind-key "S-<delete>")
  (unbind-key "S-<insert>")
  (unbind-key "S-<insertchar>"))
#+end_src

*** Better Defaults

#+begin_src emacs-lisp
(use-package emacs
  :custom
  (undo-limit 640000)
  (undo-outer-limit 96000000)
  (undo-strong-limit 960000)
  :config
  (unbind-key "C-S-<backspace>")
  (unbind-key "C-x C-z")
  (unbind-key "C-z")
  :bind
  (("C-S-k" . kill-whole-line)
   ([remap capitalize-word] . capitalize-dwim)
   ([remap delete-horizontal-space] . cycle-spacing)
   ([remap downcase-word] . downcase-dwim)
   ([remap query-replace] . query-replace-regexp)
   ([remap upcase-word] . upcase-dwim)
   ([remap zap-to-char] . zap-up-to-char)))
#+end_src

** Text

#+begin_src emacs-lisp
(use-package emacs
  :custom
  (colon-double-space nil)
  (fill-column 80)
  (indent-tabs-mode nil)
  (sentence-end-double-space nil)
  (standard-indent 2)
  (tab-always-indent 'complete)
  (tab-first-completion 'eol)
  (tab-width standard-indent))
#+end_src

** Minibuffer

#+begin_src emacs-lisp
(use-package emacs
  :custom (enable-recursive-minibuffers t))
#+end_src

#+begin_src emacs-lisp
(use-package mb-depth
  :config (minibuffer-depth-indicate-mode 1))
#+end_src

** Miscellaneous

#+begin_src emacs-lisp
(use-package emacs
  :custom
  (confirm-kill-emacs #'yes-or-no-p)
  (create-lockfiles nil)
  (delete-by-moving-to-trash t)
  (next-error-message-highlight t)
  (read-extended-command-predicate #'command-completion-default-include-p)
  (require-final-newline 'visit-save)
  (ring-bell-function 'ignore)
  (save-interprogram-paste-before-kill t)
  (uniquify-buffer-name-style 'forward)
  (use-dialog-box nil)
  (use-short-answers t)
  :config
  (column-number-mode 1)
  (line-number-mode 1)
  (put 'narrow-to-region 'disabled nil)
  :hook
  ((c++-mode c-mode) . (lambda () (c-toggle-comment-style -1)))
  (after-save . executable-make-buffer-file-executable-if-script-p)
  (find-file . size-indication-mode))
#+end_src

* Custom Functions

** Configuration Management

#+begin_src emacs-lisp
(use-package emacs
  :preface
  (defun my-config-find-file (filename)
    "Edit config file FILENAME."
    (interactive
     (let ((config-files
            (append `(,early-init-file ,user-init-file) my-config--org-files))
           (collection
            (lambda (string predicate action)
              (if (eq action 'metadata)
                  '(metadata (category . file))
                (complete-with-action action config-files string predicate)))))
       (list (completing-read
              "Find config file: " collection nil 'require-match))))
    (find-file filename))
  (defun my-config-reload ()
    "Reload all config files."
    (interactive)
    (load user-init-file))
  :bind
  (:prefix-map my-config-prefix-map
   :prefix "C-c d"
   ("f" . my-config-find-file)
   ("r" . my-config-reload)))
#+end_src

** Formatting

#+begin_src emacs-lisp
(use-package emacs
  :preface
  (defun my-format-buffer (&optional buffer-or-name)
    "Format BUFFER-OR-NAME, or the current buffer if omitted."
    (interactive)
    (with-current-buffer (or buffer-or-name (current-buffer))
      (my-format-region (point-min) (point-max))))
  (defun my-format-region (start end)
    "Format the region between START and END."
    (interactive "r")
    (save-excursion
      (cond ((and (fboundp #'eglot-managed-p) (eglot-managed-p))
             (eglot-format start end))
            ((and (fboundp #'shfmt-region) (derived-mode-p 'sh-mode))
             (shfmt-region start end))
            (t
             (indent-region start end)
             (whitespace-cleanup-region start end)))))
  (defun my-sort-lines-fold-case ()
    "Wrapper around `sort-lines' with `sort-fold-case' set to t."
    (interactive)
    (let ((sort-fold-case t))
      (call-interactively #'sort-lines))))
#+end_src

** Message Composing

#+begin_src emacs-lisp
(use-package emacs
  :preface
  (defun my-message-compose-mode ()
    (my-set-fill-column 72)
    (auto-fill-mode 1)))
#+end_src

** Text

#+begin_src emacs-lisp
(use-package emacs
  :preface
  (defun my-set-fill-column (n)
    (setq-local fill-column n)
    (setq-local whitespace-line-column nil)
    (when (bound-and-true-p whitespace-mode)
      (whitespace-mode 0)
      (whitespace-mode 1))))
#+end_src

* Packages

** Built-in

*** autorevert

#+begin_src emacs-lisp
(use-package autorevert
  :custom
  (global-auto-revert-non-file-buffers t)
  :config
  (add-to-list 'global-auto-revert-ignore-modes 'Buffer-menu-mode)
  (global-auto-revert-mode 1))
#+end_src

*** bookmark

#+begin_src emacs-lisp
(use-package bookmark
  :custom (bookmark-save-flag 1))
#+end_src

*** cc-mode

#+begin_src emacs-lisp
(use-package cc-mode
  :custom
  (c-basic-offset standard-indent)
  :config
  (define-advice c-indent-line-or-region (:around (f &rest args) complete)
    (let ((saved-point (point)))
      (apply f args)
      (when (= saved-point (point))
        (completion-at-point)))))
#+end_src

*** comp

#+begin_src emacs-lisp
(use-package comp
  :custom (native-comp-async-report-warnings-errors 'silent))
#+end_src

*** compile

#+begin_src emacs-lisp
(use-package compile
  :custom
  (compilation-ask-about-save nil)
  (compilation-scroll-output 'first-error)
  (compile-command nil)
  :config
  (add-hook 'compilation-filter-hook #'ansi-color-compilation-filter)
  :bind
  (:prefix-map my-compile-prefix-map
   :prefix "C-c c"
   ("c" . recompile)))
#+end_src

*** dabbrev

#+begin_src emacs-lisp
(use-package dabbrev
  :bind*
  ("C-M-i" . dabbrev-completion))
#+end_src

*** dired

#+begin_src emacs-lisp
(use-package dired
  :preface
  (defun my-dired-xdg-open-file ()
    "Open file with preferred application using xdg-open."
    (interactive)
    (let ((file (dired-get-filename nil t)))
      (call-process "xdg-open" nil 0 nil file)))
  :custom
  (dired-do-revert-buffer t)
  (dired-dwim-target t)
  (dired-kill-when-opening-new-dired-buffer t)
  (dired-recursive-copies 'always)
  (dired-recursive-deletes 'always)
  (dired-vc-rename-file t)
  :bind
  (:map dired-mode-map
   ("C-<return>" . my-dired-xdg-open-file)))
#+end_src

*** display-fill-column-indicator

#+begin_src emacs-lisp
(use-package display-fill-column-indicator
  :hook ((find-file prog-mode text-mode) . display-fill-column-indicator-mode))
#+end_src

*** display-line-numbers

#+begin_src emacs-lisp
(use-package display-line-numbers
  :preface
  (defun my-display-line-numbers-mode ()
    (when (buffer-file-name)
      (display-line-numbers-mode 1)))
  :custom
  (display-line-numbers-widen t)
  :hook
  ((prog-mode text-mode) . my-display-line-numbers-mode))
#+end_src

*** doc-view

#+begin_src emacs-lisp
(use-package doc-view
  :custom (doc-view-continuous t))
#+end_src

*** ediff

#+begin_src emacs-lisp
(use-package ediff
  :custom
  (ediff-keep-variants nil)
  (ediff-split-window-function #'split-window-horizontally)
  (ediff-window-setup-function #'ediff-setup-windows-plain))
#+end_src

*** eldoc

#+begin_src emacs-lisp
(use-package eldoc
  :custom (eldoc-echo-area-use-multiline-p nil))
#+end_src

*** elisp-mode

#+begin_src emacs-lisp
(use-package elisp-mode
  :hook
  (emacs-lisp-mode . (lambda () (setq-local sentence-end-double-space t))))
#+end_src

*** flyspell

#+begin_src emacs-lisp
(use-package flyspell
  :hook
  (prog-mode . flyspell-prog-mode)
  (text-mode . flyspell-mode))
#+end_src

*** help-at-point

#+begin_src emacs-lisp
(use-package help-at-point
  :bind ("C-c h" . display-local-help))
#+end_src

*** hideshow

#+begin_src emacs-lisp
(use-package hideshow
  :hook (prog-mode . hs-minor-mode))
#+end_src

*** hl-line

#+begin_src emacs-lisp
(use-package hl-line
  :preface
  (defun my-inhibit-global-hl-line-mode ()
    (setq-local global-hl-line-mode nil))
  :config
  (global-hl-line-mode 1))
#+end_src

*** js

#+begin_src emacs-lisp
(use-package js
  :custom (js-indent-level standard-indent))
#+end_src

*** org

#+begin_src emacs-lisp
(use-package org
  :custom
  (org-adapt-indentation nil)
  (org-catch-invisible-edits 'error)
  (org-edit-src-content-indentation 0)
  (org-src-preserve-indentation t)
  :config
  (unbind-key "C-c '" 'org-src-mode-map)
  :bind
  (:map org-src-mode-map
   ("C-c C-c" . org-edit-src-exit)))
#+end_src

*** paren

#+begin_src emacs-lisp
(use-package paren
  :custom (show-paren-delay 0))
#+end_src

*** project

#+begin_src emacs-lisp
(use-package project
  :preface
  (cl-defgeneric my-project-compile-dir (project)
    (defvar my-project-compile-dir nil)
    (cond
     (my-project-compile-dir)
     ((when-let ((dir (getenv "BUILD_DIR")))
        (expand-file-name dir (project-root project))))
     ((my-project-workspace-dir project))))
  (defun my-project-compile-advice (fn &rest args)
    (let* ((project (project-current t))
           (name (project-name project))
           (compile (my-project-compile-dir project))
           (root (project-root project)))
      (cl-letf (((symbol-function 'project-remember-project) #'ignore)
                ((symbol-function 'project-current)
                 (lambda (&rest _) project))
                ((symbol-function 'project-root)
                 (lambda (&rest _) (or compile root))))
        (apply fn args))))
  (cl-defgeneric my-project-workspace-dir (project)
    (defvar my-project-workspace-dir nil)
    (or my-project-workspace-dir (project-root project)))
  (defun my-project-file-name ()
    (when-let ((name (buffer-file-name))
               (project (project-current)))
      (file-relative-name name (my-project-workspace-dir project))))
  (defun my-project-mode-line ()
    (defvar my-project-mode-line--value nil)
    (unless my-project-mode-line--value
      (when-let ((project (project-current)))
        (setq-local
         my-project-mode-line--value
         (concat " P:" (project-name project)))))
    my-project-mode-line--value)
  (defun my-project-prefixed-buffer-name (mode)
    (concat "*"
            (or name (project-name (project-current)))
            "-"
            (downcase mode)
            "*"))
  (cl-defmethod project-root ((project (head manual)))
    (cdr project))
  (defun my-project-try-manual (dir)
    (when-let ((root (locate-dominating-file dir ".project")))
      (cons 'manual root)))
  :custom
  (project-compilation-buffer-name-function #'my-project-prefixed-buffer-name)
  (project-switch-commands
   '((project-dired "Dired")
     (project-shell "Shell")))
  :config
  (add-hook 'project-find-functions #'my-project-try-manual)
  (add-to-list 'mode-line-misc-info '(:eval (my-project-mode-line)))
  (advice-add 'project-compile :around #'my-project-compile-advice))
#+end_src

*** python

#+begin_src emacs-lisp
(use-package python
  :preface
  (defun my-python-mode ()
    (my-set-fill-column 88)
    (setq-local tab-width python-indent-offset))
  :custom
  (python-indent-guess-indent-offset nil)
  :hook
  (python-mode . my-python-mode))
#+end_src

*** recentf

#+begin_src emacs-lisp
(use-package recentf
  :custom (recentf-max-saved-items 50)
  :config (recentf-mode 1))
#+end_src

*** repeat

#+begin_src emacs-lisp
(use-package repeat
  :config (repeat-mode 1))
#+end_src

*** savehist

#+begin_src emacs-lisp
(use-package savehist
  :config (savehist-mode 1))
#+end_src

*** saveplace

#+begin_src emacs-lisp
(use-package saveplace
  :config (save-place-mode 1))
#+end_src

*** sh-script

#+begin_src emacs-lisp
(use-package sh-script
  :custom (sh-basic-offset standard-indent))
#+end_src

*** smerge-mode

#+begin_src emacs-lisp
(use-package smerge-mode
  :custom
  (smerge-command-prefix (kbd "C-c v"))
  :bind
  (:repeat-map my-smerge-repeat-map
   ("a" . smerge-keep-all)
   ("b" . smerge-keep-base)
   ("l" . smerge-keep-lower)
   ("n" . smerge-next)
   ("p" . smerge-prev)
   ("u" . smerge-keep-upper)))
#+end_src

*** so-long

#+begin_src emacs-lisp
(use-package so-long
  :config (global-so-long-mode 1))
#+end_src

*** subword

#+begin_src emacs-lisp
(use-package subword
  :hook (prog-mode . subword-mode))
#+end_src

*** whitespace

#+begin_src emacs-lisp
(use-package whitespace
  :preface
  (defun my-whitespace-reload ()
    (whitespace-turn-off)
    (whitespace-turn-on-if-enabled))
  :custom
  (whitespace-line-column nil)
  (whitespace-style '(face
                      trailing
                      lines-tail
                      missing-newline-at-eof
                      empty
                      indentation::space
                      space-after-tab
                      space-before-tab
                      tab-mark))
  :hook
  ((find-file prog-mode text-mode) . whitespace-mode)
  (after-save . my-whitespace-reload))
#+end_src

*** windmove

#+begin_src emacs-lisp
(use-package windmove
  :custom
  (windmove-wrap-around t)
  :bind
  (("M-s-H" . windmove-swap-states-left)
   ("M-s-J" . windmove-swap-states-down)
   ("M-s-K" . windmove-swap-states-up)
   ("M-s-L" . windmove-swap-states-right)
   ("M-s-h" . windmove-left)
   ("M-s-j" . windmove-down)
   ("M-s-k" . windmove-up)
   ("M-s-l" . windmove-right)))
#+end_src

*** window

#+begin_src emacs-lisp
(use-package window
  :preface
  (defun my-fit-window-to-fill-column ()
    "Fit the currently selected window with respect to `fill-column'."
    (interactive)
    (let ((fit-window-to-buffer-horizontally 'only)
          (width (1+ fill-column)))
      (when (bound-and-true-p display-line-numbers-mode)
        (setq width (+ width (truncate (line-number-display-width 'columns)))))
      (unless (display-graphic-p)
        (setq width (+1 width)))
      (fit-window-to-buffer nil nil nil width width)))
  (defun my-split-window-sensibly (&optional window)
    (let* ((root (window-main-window))
           (root-width (window-total-width root))
           (num-h (1+ (window-combinations root :horizontal)))
           (min-width (/ split-width-threshold 2))
           (root-height (window-total-height root))
           (num-v (1+ (window-combinations root)))
           (min-height (/ split-height-threshold 2)))
      (cond
       ((< (* num-h min-width) root-width)
        (message "right")
        (split-window-right nil window))
       ((< (* num-v min-height) root-height)
        (message "below")
        (split-window-below nil window))
       (t
        (message "default")
        (split-window-sensibly window)))))
  (defun my-window-dedicated-mode-line ()
    (when (window-dedicated-p)
      " D"))
  :custom
  (fit-window-to-buffer-horizontally t)
  (hscroll-margin 1)
  (split-height-threshold (* 2 24))
  (split-width-threshold (* 2 80))
  (split-window-preferred-function #'my-split-window-sensibly)
  (switch-to-buffer-obey-display-actions t)
  (window-combination-resize t)
  :config
  (setq my-side-window-action-alist
        '((window-width . fit-window-to-buffer)
          (window-parameters . ((no-delete-other-windows . t)))))
  (add-to-list 'display-buffer-alist
               `(,(rx "*" (* nonl)
                      (| "Help"
                         "Man"
                         "Metahelp"
                         "eldoc"
                         "info")
                      (* nonl) "*")
                 (display-buffer-reuse-window display-buffer-in-side-window)
                 (side . right)
                 (slot . 0)
                 ,@my-side-window-action-alist))
  (add-to-list 'display-buffer-alist
               `(,(rx "*" (* nonl)
                      (| "Embark"
                         "Occur"
                         "compilation"
                         "envrc")
                      (* nonl) "*")
                 (display-buffer-reuse-window display-buffer-in-side-window)
                 (side . right)
                 (slot . 1)
                 ,@my-side-window-action-alist))
  (add-to-list 'display-buffer-alist
               `(,(rx "*" (* nonl)
                      (| "Python"
                         "eshell"
                         "shell"
                         "vterm")
                      (* nonl) "*")
                 (display-buffer-reuse-window display-buffer-in-direction)
                 (direction . bottom)
                 (window-height . 0.25)))
  (add-to-list 'mode-line-misc-info '(:eval (my-window-dedicated-mode-line)))
  :bind
  (("C-c q" . window-toggle-side-windows)
   :prefix-map my-window-prefix-map
   :prefix "C-c w"
   ("f" . my-fit-window-to-fill-column)))
#+end_src

*** winner

#+begin_src emacs-lisp
(use-package winner
  :after window
  :config
  (unbind-key "C-c <left>")
  (unbind-key "C-c <right>")
  (winner-mode 1)
  :bind
  (:map my-window-prefix-map
   ("n" . winner-redo)
   ("p" . winner-undo)
   :repeat-map winner-repeat-map
   ("n" . winner-redo)
   ("p" . winner-undo)))
#+end_src

** External

*** cape

#+begin_src emacs-lisp
(use-package cape
  :ensure t
  :demand t
  :config
  (add-to-list 'completion-at-point-functions #'cape-dabbrev)
  (add-to-list 'completion-at-point-functions #'cape-file)
  :bind-keymap
  ("C-c TAB" . cape-prefix-map))
#+end_src

*** consult

#+begin_src emacs-lisp
(use-package consult
  :ensure t
  :custom
  (consult-narrow-key "<")
  (register-preview-delay 0)
  (register-preview-function #'consult-register-format)
  (xref-show-definitions-function #'consult-xref)
  (xref-show-xrefs-function #'consult-xref)
  :config
  (add-to-list 'consult-preview-excluded-files (rx any "." (| "asc" "gpg") eol))
  (advice-add #'register-preview :override #'consult-register-window)
  :bind
  (([remap bookmark-jump] . consult-bookmark)
   ([remap goto-line] . consult-goto-line)
   ([remap isearch-backward] . consult-line)
   ([remap isearch-forward] . consult-line)
   ([remap jump-to-register] . consult-register)
   ([remap project-switch-to-buffer] . consult-project-buffer)
   ([remap repeat-complex-command] . consult-complex-command)
   ([remap switch-to-buffer-other-frame] . consult-buffer-other-frame)
   ([remap switch-to-buffer-other-window] . consult-buffer-other-window)
   ([remap switch-to-buffer] . consult-buffer)
   ([remap yank-pop] . consult-yank-pop)
   :prefix-map my-buffer-prefix-map
   :prefix "C-c b"
   ("I" . consult-imenu-multi)
   ("M" . consult-man)
   ("SPC" . consult-global-mark)
   ("e" . consult-compile-error)
   ("f" . consult-flymake)
   ("i" . consult-imenu)
   ("m" . consult-mark)
   ("o" . consult-outline)
   :prefix-map my-search-prefix-map
   :prefix "C-c s"
   ("F" . consult-find)
   ("G" . consult-grep)
   ("L" . consult-line-multi)
   ("f" . consult-fd)
   ("g" . consult-ripgrep)
   ("k" . consult-keep-lines)
   ("l" . consult-line)
   ("u" . consult-focus-lines)
   ("v" . consult-git-grep)
   :prefix-map my-macro-prefix-map
   :prefix "C-c x"
   ("m" . consult-kmacro)
   ("r" . consult-register-store)
   :map minibuffer-local-map
   ([remap next-matching-history-element] . consult-history)
   ([remap previous-matching-history-element] . consult-history)))
#+end_src

*** corfu

#+begin_src emacs-lisp
(use-package corfu
  :ensure t
  :demand t
  :custom
  (corfu-cycle t)
  (corfu-echo-documentation t)
  :config
  (add-to-list 'savehist-additional-variables 'corfu-history)
  (corfu-history-mode 1)
  (corfu-popupinfo-mode 1)
  (global-corfu-mode 1)
  :bind
  (:map corfu-map
   ("SPC" . corfu-insert-separator)))
#+end_src

*** corfu-terminal

#+begin_src emacs-lisp
(use-package corfu-terminal
  :ensure t
  :preface
  (defun my-corfu-terminal-mode (&optional frame)
    (unless (display-graphic-p frame)
      (corfu-terminal-mode 1)))
  :init
  (add-hook 'after-init-hook #'my-corfu-terminal-mode)
  (add-hook 'after-make-frame-functions #'my-corfu-terminal-mode))
#+end_src

*** edit-indirect

#+begin_src emacs-lisp
(use-package edit-indirect
  :ensure t)
#+end_src

*** eglot

#+begin_src emacs-lisp
(use-package eglot
  :ensure t
  :preface
  (defun my-eglot-managed-mode ()
    (setq-local
     completion-at-point-functions
     (cons (cape-capf-super #'tempel-complete #'eglot-completion-at-point)
           completion-at-point-functions))
    (flycheck-mode -1))
  :custom
  (eglot-events-buffer-size 0)
  (eglot-ignored-server-capabilities '(:inlayHintProvider))
  :hook
  ((c++-mode c-mode python-mode rust-mode) . eglot-ensure)
  (eglot-managed-mode . my-eglot-managed-mode)
  :bind
  (:map eglot-mode-map
   :prefix-map my-eglot-prefix-map
   :prefix "C-c l"
   ("a" . eglot-code-actions)
   ("d" . eglot-find-declaration)
   ("f" . eglot-format-buffer)
   ("r" . eglot-rename)))
#+end_src

#+begin_src emacs-lisp
(use-package consult-eglot
  :ensure t
  :after consult
  :bind
  (:map eglot-mode-map
   ([remap xref-find-apropos] . consult-eglot-symbols)))
#+end_src

*** embark

#+begin_src emacs-lisp
(use-package embark
  :ensure t
  :preface
  (defun my-embark-target-current-buffer ()
    `(current-buffer ,(buffer-name)))
  (defun my-embark-target-ietf-doc ()
    (save-match-data
      (when (thing-at-point-looking-at
             (rx (| "RFC" "rfc") (opt space) (group (+ digit))))
        `(url
          ,(format "https://datatracker.ietf.org/doc/html/rfc%s"
                   (match-string 1))
          ,(match-beginning 0) . ,(match-end 0)))))
  :init
  (setq prefix-help-command #'embark-prefix-help-command)
  :custom
  (embark-indicators
   '(embark-minimal-indicator
     embark-highlight-indicator
     embark-isearch-highlight-indicator))
  :config
  (add-to-list 'embark-target-finders #'my-embark-target-current-buffer 'append)
  (add-to-list 'embark-target-finders #'my-embark-target-ietf-doc)
  (defvar-keymap my-embark-current-buffer-map
    :doc "Keymap for Embark actions on current buffer."
    :parent embark-general-map
    "c" #'clone-buffer
    "f" #'my-format-buffer
    "g" #'revert-buffer
    "r" #'embark-rename-buffer
    "|" #'embark-shell-command-on-buffer)
  (add-to-list 'embark-keymap-alist
               '(current-buffer . my-embark-current-buffer-map))
  (add-to-list 'embark-repeat-actions #'string-inflection-all-cycle)
  :bind
  (:map embark-expression-map
   ("f" . my-format-region)
   :map embark-identifier-map
   ("-" . string-inflection-all-cycle)
   :map embark-region-map
   ("F" . fill-region)
   ("f" . my-format-region)
   :map embark-sort-map
   ("L" . my-sort-lines-fold-case))
  :bind*
  (("C-M-/" . embark-dwim)
   ("M-/" . embark-act)))
#+end_src

#+begin_src emacs-lisp
(use-package embark-consult
  :ensure t)
#+end_src

*** envrc

#+begin_src emacs-lisp
(use-package envrc
  :ensure t
  :config (envrc-global-mode))
#+end_src

*** exec-path-from-shell

Use this as a workaround until I've sorted out if shell or systemd should be the
source of truth for environment variables.

#+begin_src emacs-lisp
(use-package exec-path-from-shell
  :ensure t
  :config
  (when (daemonp)
    (exec-path-from-shell-initialize)))
#+end_src

*** flycheck

#+begin_src emacs-lisp
(use-package flycheck
  :ensure t
  :custom (flycheck-disabled-checkers '(yaml-ruby))
  :config (global-flycheck-mode 1))
#+end_src

#+begin_src emacs-lisp
(use-package consult-flycheck
  :ensure t
  :bind
  (:map my-buffer-prefix-map
   ("F" . consult-flycheck)))
#+end_src

*** flyspell-correct

#+begin_src emacs-lisp
(use-package flyspell-correct
  :ensure t
  :after flyspell
  :bind ([remap ispell-word] . flyspell-correct-wrapper))
#+end_src

*** gptel

#+begin_src emacs-lisp
(use-package gptel
  :ensure t
  :preface
  (defun my-gptel ()
    "If a gptel buffer already exists, switch to it. Otherwise, call `gptel'."
    (interactive)
    (let ((gptel-buffers (seq-filter
                          (lambda (b) (with-current-buffer b
                                        (bound-and-true-p gptel-mode)))
                          (buffer-list))))
      (if (= (length gptel-buffers) 1)
          (pop-to-buffer (car gptel-buffers))
        (call-interactively #'gptel))))
  (defun my-gptel-abort ()
    "Abort gptel and then call `keyboard-quit'."
    (interactive)
    (call-interactively #'gptel-abort)
    (call-interactively #'keyboard-quit))
  (defun my-gptel-mode ()
    (set-fill-column -1)
    (setq-local truncate-lines nil))
  :init
  (setopt gptel-backend nil)
  (setopt gptel-model nil)
  (setq gptel--openai nil)
  :custom
  (gptel-default-mode 'org-mode)
  :hook
  ((gptel-mode . my-gptel-mode)
   (gptel-post-stream . gptel-auto-scroll)
   (gptel-post-response . gptel-end-of-response))
  :bind
  (:prefix-map my-gptel-prefix-map
   :prefix "C-c i"
   ("c" . gptel-context-add)
   ("g" . gptel-abort)
   ("i" . my-gptel)
   ("m" . gptel-menu)
   ("r" . gptel-rewrite)
   ("u" . gptel-context-remove-all)
   :map gptel-mode-map
   ("<S-return>" . newline)
   ("<return>" . gptel-send)
   ("C-g" . my-gptel-abort)))
#+end_src

#+begin_src emacs-lisp
(use-package gptel-context
  :after gptel)
#+end_src

*** gtk-variant

#+begin_src emacs-lisp
(use-package gtk-variant
  :if (eq window-system 'x)
  :ensure t
  :hook ((server-after-make-frame window-setup) . gtk-variant-set-frame))
#+end_src

*** hl-todo

#+begin_src emacs-lisp
(use-package hl-todo
  :ensure t
  :config
  (add-hook 'magit-log-wash-summary-hook #'hl-todo-search-and-highlight)
  (add-hook 'magit-revision-wash-message-hook #'hl-todo-search-and-highlight)
  (global-hl-todo-mode 1))
#+end_src

*** json-mode

#+begin_src emacs-lisp
(use-package json-mode
  :ensure t)
#+end_src

*** keyfreq

#+begin_src emacs-lisp
(use-package keyfreq
  :ensure t
  :custom
  (keyfreq-excluded-commands '(self-insert-command))
  (keyfreq-file (expand-file-name (locate-user-emacs-file "keyfreq")))
  :config
  (keyfreq-autosave-mode 1)
  (keyfreq-mode 1))
#+end_src

*** kind-icon

#+begin_src emacs-lisp
(use-package kind-icon
  :ensure t
  :after corfu
  :custom
  (kind-icon-blend-frac 0)
  (kind-icon-default-face 'corfu-default)
  (kind-icon-use-icons nil)
  :config
  (add-to-list 'corfu-margin-formatters #'kind-icon-margin-formatter))
#+end_src

*** magit

#+begin_src emacs-lisp
(use-package magit
  :ensure t
  :init
  (add-to-list
   'project-switch-commands '(magit-project-status "Magit" "g") 'append)
  :custom
  (magit-define-global-key-bindings 'recommended)
  (magit-diff-refine-hunk 'all)
  (magit-save-repository-buffers 'dontask)
  :hook
  (git-commit-mode . my-message-compose-mode))
#+end_src

*** marginalia

#+begin_src emacs-lisp
(use-package marginalia
  :ensure t
  :config (marginalia-mode 1))
#+end_src

*** markdown-mode

#+begin_src emacs-lisp
(use-package markdown-mode
  :ensure t
  :custom
  (markdown-command "pandoc")
  (markdown-fontify-code-blocks-natively t)
  (markdown-list-indent-width standard-indent)
  (markdown-spaces-after-code-fence 0))
#+end_src

*** modus-theme

#+begin_src emacs-lisp
(use-package modus-themes
  :ensure t
  :custom
  (modus-themes-org-blocks 'gray-background)
  (modus-themes-common-palette-overrides
   '((border-mode-line-active unspecified)
     (border-mode-line-inactive unspecified)
     (bg-line-number-active unspecified)
     (bg-line-number-inactive unspecified)
     (fg-line-number-active fg-main)
     (fg-line-number-inactive fg-dim)
     (fg-region unspecified)))
  (modus-operandi-palette-overrides
   '((bg-main "#EDEDED")
     (bg-dim "#FAFAFA")
     (bg-active"#B5B5B5")
     (bg-inactive "#CFCFCF")
     (border "#929292")))
  (modus-vivendi-palette-overrides
   '((bg-main "#171717")
     (bg-dim "#2A2A2A")
     (bg-active"#5D5D5D")
     (bg-inactive "#3A3A3A")
     (border "#6E6E6E")))
  :config
  (load-theme 'modus-vivendi :no-confirm))
#+end_src

The =bg-main= color have been overridden to match my terminal background color.
Other background colors have been adjusted accordingly to retain the contrast
ratio.

| bg-main     |         | #FFFFFF | #EDEDED |
|-------------+---------+---------+---------|
| bg-dim      | #F2F2F2 |    1.12 |         |
| bg-active   | #C4C4C4 |    1.74 |         |
| bg-inactive | #E0E0E0 |    1.32 |         |
| border      | #9F9F9F |    2.65 |         |
|-------------+---------+---------+---------|
| bg-dim      | #FAFAFA |         |    1.12 |
| bg-active   | #B5B5B5 |         |    1.75 |
| bg-inactive | #CFCFCF |         |    1.33 |
| border      | #929292 |         |    2.66 |
#+tblfm: @2$3..@5$3='(modus-themes-contrast @1$3 $2);%0.2f::@6$4..@9$4='(modus-themes-contrast @1$4 $2);%0.2f

| bg-main     |         | #000000 | #171717 |
|-------------+---------+---------+---------|
| bg-dim      | #1E1E1E |    1.26 |         |
| bg-active   | #535353 |    2.73 |         |
| bg-inactive | #303030 |    1.59 |         |
| border      | #646464 |    3.55 |         |
|-------------+---------+---------+---------|
| bg-dim      | #2A2A2A |         |    1.25 |
| bg-active   | #5D5D5D |         |    2.72 |
| bg-inactive | #3A3A3A |         |    1.58 |
| border      | #6E6E6E |         |    3.52 |
#+tblfm: @2$3..@5$3='(modus-themes-contrast @1$3 $2);%0.2f::@6$4..@9$4='(modus-themes-contrast @1$4 $2);%0.2f

*** mood-line

#+begin_src emacs-lisp
(use-package mood-line
  :ensure t
  :custom (mood-line-show-eol-style t)
  :config (mood-line-mode 1))
#+end_src

*** move-text

#+begin_src emacs-lisp
(use-package move-text
  :ensure t
  :bind
  (("M-n" . move-text-down)
   ("M-p" . move-text-up)))
#+end_src

*** multiple-cursors

#+begin_src emacs-lisp
(use-package multiple-cursors
  :ensure t
  :bind
  (:prefix-map my-multiple-cursors-prefix-map
   :prefix "C-c m"
   ("a" . mc/mark-all-like-this-dwim)
   ("d" . mc/insert-numbers)
   ("f" . mc/mark-all-like-this-in-defun)
   ("l" . mc/insert-letters)
   ("m" . mc/edit-lines)
   ("M-n" . mc/skip-to-next-like-this)
   ("M-p" . mc/skip-to-previous-like-this)
   ("n" . mc/mark-next-like-this)
   ("p" . mc/mark-previous-like-this)
   ("u" . mc/unmark-next-like-this)
   :repeat-map my-multiple-cursors-repeat-map
   ("M-n" . mc/skip-to-next-like-this)
   ("M-p" . mc/skip-to-previous-like-this)
   ("n" . mc/mark-next-like-this)
   ("p" . mc/mark-previous-like-this)
   ("u" . mc/unmark-next-like-this)))
#+end_src

*** orderless

#+begin_src emacs-lisp
(use-package orderless
  :ensure t
  :custom
  (completion-styles '(tab orderless basic))
  (completion-category-overrides '((file (styles basic partial-completion))))
  :config
  (add-to-list 'completion-styles-alist
               '(tab completion-basic-try-completion ignore)))
#+end_src

*** rust-mode

#+begin_src emacs-lisp
(use-package rust-mode
  :ensure t
  :preface
  (defun my-rust-mode ()
    (my-set-fill-column 100))
  :hook
  (rust-mode . my-rust-mode))
#+end_src

*** shfmt-mode

#+begin_src emacs-lisp
(use-package shfmt
  :ensure t
  :custom
  (shfmt-arguments (list
                    "--binary-next-line"
                    "--case-indent"
                    (format "--indent=%s" standard-indent))))
#+end_src

*** string-inflection

#+begin_src emacs-lisp
(use-package string-inflection
  :ensure t
  :bind
  (("C-c S-c" . string-inflection-all-cycle)
   :repeat-map my-string-inflection-repeat-map
   ("C" . string-inflection-all-cycle)
   ("c" . string-inflection-all-cycle)))
#+end_src

*** substitute

#+begin_src emacs-lisp
(use-package substitute
  :ensure t
  :bind
  (:prefix-map my-rewrite-map
   :prefix "C-c r"
   ("F" . substitute-target-in-defun)
   ("f" . substitute-target-in-defun-and-below)
   ("n" . substitute-target-below-point)
   ("o" . substitute-target-in-outline)
   ("p" . substitute-target-above-point)
   ("r" . substitute-target-in-buffer)))
#+end_src

*** systemd

#+begin_src emacs-lisp
(use-package systemd
  :ensure t)
#+end_src

*** tempel

#+begin_src emacs-lisp
(use-package tempel
  :ensure t
  :preface
  (defun my-comment-start ()
    (if (derived-mode-p 'emacs-lisp-mode)
        ";; "
      comment-start))
  (defun my-tempel-include (elt)
    (when (eq (car-safe elt) 't)
      (when-let (template (alist-get (cadr elt) (tempel--templates)))
        (cons 'l template))))
  (defun my-tempel-setup-capf ()
    (require 'project)
    (require 'string-inflection)
    (setq-local completion-at-point-functions
                (cons #'tempel-complete completion-at-point-functions)))
  :custom
  (tempel-path (expand-file-name (locate-user-emacs-file "templates/*.eld")))
  (tempel-trigger-prefix "<")
  :config
  (add-to-list 'tempel-user-elements #'my-tempel-include)
  :hook
  ((prog-mode text-mode) . my-tempel-setup-capf)
  :bind
  (:prefix-map my-prog-prefix-map
   :prefix "C-c l"
   ("t" . tempel-insert)))
#+end_src

*** vertico

#+begin_src emacs-lisp
(use-package vertico
  :ensure t
  :demand t
  :custom
  (vertico-cycle t)
  :config
  (vertico-mode 1)
  (vertico-multiform-mode 1)
  :bind
  (:map vertico-map
   ("C-M-n" . vertico-next-group)
   ("C-M-p" . vertico-previous-group)))
#+end_src

*** vterm

#+begin_src emacs-lisp
(use-package vterm
  :ensure t
  :preface
  (defun my-project-vterm ()
    "Open a Vterm buffer in project."
    (interactive)
    (let* ((default-directory (project-root (project-current t)))
           (current-prefix-arg (project-prefixed-buffer-name "vterm"))
           (buffer (get-buffer current-prefix-arg)))
      (call-interactively #'vterm)))
  :init
  (add-to-list
   'project-switch-commands '(my-project-vterm "Vterm" "RET") 'append)
  :custom
  (vterm-max-scrollback 100000)
  :config
  (add-hook 'vterm-mode-hook #'my-inhibit-global-hl-line-mode)
  :bind
  (("C-c <return>" . vterm)
   ("C-c RET" . vterm)))
#+end_src

*** wgrep

#+begin_src emacs-lisp
(use-package wgrep
  :ensure t)
#+end_src

*** whole-line-or-region

#+begin_src emacs-lisp
(use-package whole-line-or-region
  :ensure t
  :after embark
  :preface
  (defun my-embark-inhibit-wlr-advice (fn &rest args)
    (let ((whole-line-or-region-local-mode nil))
      (apply fn args)))
  :config
  (advice-add 'embark-act :around #'my-embark-inhibit-wlr-advice)
  (whole-line-or-region-global-mode 1))
#+end_src

*** yaml-mode

#+begin_src emacs-lisp
(use-package yaml-mode
  :ensure t
  :custom (yaml-indent-offset standard-indent))
#+end_src
